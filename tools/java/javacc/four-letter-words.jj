/* four-letter-words.jj A simple report writer. */
options {
	STATIC = false ;
}

PARSER_BEGIN(FLW)
	import java.io.Reader ;
	import java.io.StringReader ;
	
	class FLW {
		static String substitute( String inString ) {
			Reader reader = new StringReader( inString ) ;
			FLW parser = new FLW( reader ) ;
			StringBuffer buffer = new StringBuffer() ;
			
			try {
				parser.Start( buffer ) ; 
			}
			catch( TokenMgrError e ) {
				throw new IllegalStateException() ; 
			}
			catch( ParseException e ) {
				throw new IllegalStateException() ; 
			}

			return buffer.toString() ; 
		}
		
		public static void main( String[] args ) throws ParseException, TokenMgrError {
			String str = String.join(" ", args);
			System.out.println("Result: " + FLW.substitute(str)) ;			
		}
	}
PARSER_END(FLW)


TOKEN : { < #LETTER : ["a"-"z","A"-"Z"] > }
TOKEN : { < FIVE_OR_MORE_LETTER_WORD : (<LETTER>){5} (<LETTER>)* > }
TOKEN : { < FOUR_LETTER_WORD : (<LETTER>){4} > }
TOKEN : { < OTHER : ~[] > }


void Start( StringBuffer buffer ) :
{
	Token t ;
}
{
	(
		<FOUR_LETTER_WORD>
		{ buffer.append("****"); }
	|
		( t=<FIVE_OR_MORE_LETTER_WORD> | t=<OTHER> )
		{ buffer.append( t.image ) ; }
	)*
	<EOF>
}
